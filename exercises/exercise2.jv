pipeline TreeProcessingPipeline {

    TreeFileFetcher -> TreeTextInterpreter;

    TreeTextInterpreter
        -> TreeCSVInterpreter 
        -> ColumnRemover
        -> TreeTableInterpreter
        -> TreeDataLoader;


    valuetype stadtteil oftype text {
        constraints: [districtvogelsang];
    }

    valuetype ID oftype text {
        constraints: [geoloc];
    }

    constraint geoloc on text:
        value matches /^(\d{1,3}\.\d+),\s(\d{1,3}\.\d+)$/;

    constraint districtvogelsang on text:
        value matches /^Vogelsang.*$/;

    // Extract
    block TreeFileFetcher oftype HttpExtractor {
        url: 'https://opendata.rhein-kreis-neuss.de/api/v2/catalog/datasets/stadt-neuss-herbstpflanzung-2023/exports/csv';
        retries: 2;
    }


    block TreeTextInterpreter oftype TextFileInterpreter { 
        encoding: "utf8";
        lineBreak: /\r?\n/;
    }

    block TreeCSVInterpreter oftype CSVInterpreter {
        delimiter : ";";
        enclosing: "";
        enclosingEscape : "";
    }
    //  Transform
    block ColumnRemover oftype ColumnDeleter {
        delete: [column E];
    }

    block TreeTableInterpreter oftype TableInterpreter {
        header: true;
        columns : [
            "lfd_nr" oftype integer,
            "stadtteil" oftype stadtteil, // custom type with constraints
            "standort" oftype text,
            "baumart_botanisch" oftype text,
            "baumart_deutsch" oftype text,
            "id" oftype ID, // custom type with constraints
            "baumfamilie" oftype text,
        ];
    }

    // Load
    block TreeDataLoader oftype SQLiteLoader {
        table: 'trees';
        file: './trees.sqlite';
    }
}
