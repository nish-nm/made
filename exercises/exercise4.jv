constraint isvalidID oftype RangeConstraint {
    lowerBound: 0;
}

    valuetype valid_ID oftype integer {
    constraints: [
        isvalidID,
    ];
}
pipeline ETLpipeline {

    DataExtract
        -> DataArchiveInterpreter
        -> DataFile
        -> DataFileInterpreter
        -> DataCSV
        -> CellRangeSelector
        -> DataColumnDelete
        -> DataHeaderRename
        -> DataTableInterpreter
        -> DataTransformer
        -> DataTableTransform
        -> toSqLite;

    
    transform temperatureunit {
        from celsius oftype decimal;
        to fahrenheit oftype decimal;
        fahrenheit: (celsius * 9/5) + 32;
    }


    block DataExtract oftype HttpExtractor {
        url: "https://www.mowesta.com/data/measure/mowesta-dataset-20221107.zip";
    }

    block DataArchiveInterpreter oftype ArchiveInterpreter {
        archiveType: "zip";
    }

    block DataFile oftype FilePicker {
        path: "./data.csv";
    }

    block DataFileInterpreter oftype TextFileInterpreter { }

    block DataCSV oftype CSVInterpreter {
        delimiter: ";";
    }

    block CellRangeSelector oftype CellRangeSelector {
        select: range A1:J*;
    }

    block DataColumnDelete oftype ColumnDeleter {
        delete: [ column F, column G, column H, column I
        ];
    }

    block DataHeaderRename oftype CellWriter {
        at: range A1:F1;
        write: [
            "id",
            "producer",
            "model",
            "month",
            "temperature",
            "battery_temperature"
        ];
    }

    block DataTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "id" oftype valid_ID,
            "producer" oftype text,
            "model" oftype text,
            "month" oftype integer,
            "temperature" oftype decimal,
            "battery_temperature" oftype decimal
        ];
    }

    
    block DataTransformer oftype TableTransformer {
        inputColumns: [
            'temperature'
        ];
        outputColumn: 'temperature';
        uses: temperatureunit;
    }

    block DataTableTransform oftype TableTransformer {
        inputColumns: [
            'battery_temperature'
        ];
        outputColumn: 'battery_temperature';
        uses: temperatureunit;
    }

    block toSqLite oftype SQLiteLoader {
        table: "temperatures";
        file: "./temperatures.sqlite";
    }
}

